expression %player% has guild:
    return type: boolean
    get:
        set {_return} to false
        load yaml "plugins/guilds/data/players/%expression-1%.yml" as "%expression-1%.player"
        set {_return} to yaml value "guild" from "%expression-1%.player"
        unload yaml "%expression-1%.player"
        if {_return} is not "brak":
            set {_return} to true
        return {_return}

expression guild tag %text% exists:
    return type: boolean
    get:
        if yaml file "plugins/guilds/data/guilds/%expression-1%.yml" doesn't exist:
            set {_return} to false
        else:
            set {_return} to true
        return {_return}

expression guild name %text% exists:
    return type: boolean
    get:
        set {_return} to false
        load all yaml from directory "plugins/guilds/data/guilds"
        loop all of the currently loaded yaml files:
            if loop-value contains "plugins/guilds/data/guilds/":
                if yaml value "name" from loop-value is expression-1:
                    set {_return} to true
            unload loop-value
        return {_return}

expression %player% guild tag:
    return type: text
    get:
        load yaml "plugins/guilds/data/players/%expression-1%.yml" as "%expression-1%.player"
        set {_return} to yaml value "guild" from "%expression-1%.player"
        unload yaml "%expression-1%.player"
        return {_return}

on join:
    if yaml file "plugins/guilds/data/players/%player%.yml" doesn't exist:
        load yaml "plugins/guilds/data/players/%player%.yml" as "%player%.player"
        set yaml value "guild" from "%player%.player" to "brak"
        set yaml value "points" from "%player%.player" to 1000
        set yaml value "kills" from "%player%.player" to 0
        set yaml value "deaths" from "%player%.player" to 0
        save yaml "%player%.player"
        unload yaml "%player%.player"
    execute console command "tab player %player% belowname"
    if player guild tag is not "brak":
        set {_g} to player guild tag
        set {_format} to {guilds::config::belowname-tag-format}
        replace all "{TAG}" with {_g} in {_format}
        execute console command "tab player %player% belowname %{_format}%"

function reloadConfig():
    load yaml "plugins/guilds/config.yml" as "guilds.config"
    loop all node from "guilds.config":
        set {guilds::config::%loop-value%} to yaml value loop-value from "guilds.config"
        set {guilds::config::blocked-regions::*} to yaml list "blocked-regions" from "guilds.config"
        set {guilds::config::required-items::*} to yaml list "required-items" from "guilds.config"
    unload yaml "guilds.config"

function reloadMessages():
    load yaml "plugins/guilds/messages.yml" as "guilds.messages"
    loop all node from "guilds.messages":
        set {guilds::messages::%loop-value%} to yaml value loop-value from "guilds.messages"
        set {guilds::messages::help-commands::*} to yaml list "help-commands" from "guilds.messages"
        set {guilds::messages::guild-info-message::*} to yaml list "guild-info-message" from "guilds.messages"
    unload yaml "guilds.messages"

on load:
    if yaml file "plugins/guilds/config.yml" doesn't exist:
        load yaml "plugins/guilds/config.yml" as "guilds.config"
        set yaml value "chat-prefix" from "guilds.config" to "&8✦"
        set the comments of yaml node "chat-prefix" from "guilds.config" to "Jak na chacie ma wyglądać prefix przed wiadomościami?"
        set yaml value "min-tag-length" from "guilds.config" to 2
        set the comments of yaml node "min-tag-length" from "guilds.config" to "Ile minimalnie musi zawierać tag gildii?"
        set yaml value "max-tag-length" from "guilds.config" to 5
        set the comments of yaml node "max-tag-length" from "guilds.config" to "Ile maksymalnie może mieć znaków tag gildii?"
        set yaml value "min-name-length" from "guilds.config" to 5
        set the comments of yaml node "min-name-length" from "guilds.config" to "Ile minimalnie musi zawierać nazwa gildii?"
        set yaml value "max-name-length" from "guilds.config" to 15
        set the comments of yaml node "max-name-length" from "guilds.config" to "Ile maksymalnie może zawierać zanków tag gildii?"
        set yaml value "belowname-tag-format" from "guilds.config" to "&8[&c{TAG}&8]"
        set the comments of yaml node "belowname-tag-format" from "guilds.config" to "Jak ma wyglądać tag pod nickiem gracza? Zmienna: {TAG} - określa tag gildii"
        set yaml list "blocked-regions" from "guilds.config" to "spawn" and "build"
        set the comments of yaml node "blocked-regions" from "guilds.config" to "Przy jakich regionach nie można tworzyć gildii?" and "Jeżeli ta opcja ma być wyłączona wpisz: none (jako pierwszą z wartości)"
        set yaml list "required-items" from "guilds.config" to "3 gold ingot" and "1 diamond"
        set the comments of yaml node "required-items" from "guilds.config" to "Jakie są wymagane przedmioty do stworzenia gildii?" and "Jeżeli ta opcja ma być wyłączona wpisz: none (jako pierwszą z wartości)"
        set yaml value "required-items-gui-name" from "guilds.config" to "{PREFIX} &3Przedmioty na gildie"
        set the comments of yaml node "required-items-gui-name" from "guilds.config" to "Jak ma wyglądać tytuł GUI dla przedmiotów na gildię?"
        save yaml "guilds.config"
        unload yaml "guilds.config"
    if yaml file "plugins/guilds/messages.yml" doesn't exist:
        load yaml "plugins/guilds/messages.yml" as "guilds.messages"
        set yaml list "help-commands" from "guilds.messages" to "{PREFIX} &8&m-=-=-=-=-=&r &8( &3&lGILDIE &8)&8&m=-=-=-=-=-&r {PREFIX}" and "{PREFIX} &3/g zaloz [tag] [nazwa] &8- &7tworzy gildie" and "{PREFIX} &3/g itemy &8- &7wyświetla przedmioty na gildie" and "{PREFIX} &3/g usun &8- &7usuwa gildię" and "{PREFIX} &3/g info [tag] &8- &7wyświetla informacje o gildii" and "{PREFIX} &3/g zapros [nick] &8- &7zaprasza podanego gracza do gildii" and "{PREFIX} &3/g dolacz [tag] &8- &7dołącza do gildii"
        set the comments of yaml node "help-commands" from "guilds.messages" to "Jak ma wyglądać komenda która wyświetla się po wpisaniu /g?" and "Zmienne:" and "  {PREFIX} - wyświetla prefix"
        set yaml value "no-tag-argument" from "guilds.messages" to "{PREFIX} &cPodaj tag gildii!"
        set the comments of yaml node "no-tag-argument" from "guilds.messages" to "##########################" and "##########################" and "##TWORZENIE GILDII##" and "##Zmienne: {PREFIX}, {MIN-TAG-LENGTH}, {MAX-TAG-LENGTH}, {MIN-NAME-LENGTH}, {MAX-NAME-LENGTH}, {PLAYER}, {TAG}, {NAME}, {ITEM}"
        set yaml value "min-tag-length" from "guilds.messages" to "{PREFIX} &cTag gildii musi zawierać przynajmniej {MIN-TAG-LENGTH} znaki!"
        set yaml value "max-tag-length" from "guilds.messages" to "{PREFIX} &cTag gildii nie może zawierać więcej niż {MAX-TAG-LENGTH} znaki!"
        set yaml value "no-name-argument" from "guilds.messages" to "{PREFIX} &cPodaj nazwę gildii!"
        set yaml value "min-name-length" from "guilds.messages" to "{PREFIX} &cNazwa gildii musi zawierać przynajmniej {MIN-NAME-LENGTH} znaków!"
        set yaml value "max-name-length" from "guilds.messages" to "{PREFIX} &cNazwa gildii nie może zawierać więcej niż {MAX-NAME-LENGTH} znaków!"
        set yaml value "player-is-in-guild" from "guilds.messages" to "{PREFIX} &cJesteś już w gildii!"
        set yaml value "guild-tag-exists" from "guilds.messages" to "{PREFIX} &cGildia o podanym tagu istnieje!"
        set yaml value "guild-name-exists" from "guilds.messages" to "{PREFIX} &cGildia o podanej nazwie istnieje!"
        set yaml value "broadcast-guild-create" from "guilds.messages" to "{PREFIX} &7Gracz &3{PLAYER} &7założył gildie o tagu &3{TAG} &7i nazwie &3{NAME}"
        set yaml value "region-contain-blocked-region" from "guilds.messages" to "{PREFIX} &cNie możesz założyć w tym miejscu gildii! Znajdź inne miejsce!"
        set yaml value "region-contain-another-guild" from "guilds.messages" to "{PREFIX} &cNie możesz założyć w tym miejscu gildii! Znajdź inne miejsce!"
        set yaml value "required-item-error" from "guilds.messages" to "{PREFIX} &cNie masz {ITEM}, aby założyć gildię!"
        set yaml value "player-hasnt-guild" from "guilds.messages" to "{PREFIX} &cNie masz żadnej gildii!"
        set the comments of yaml node "player-hasnt-guild" from "guilds.messages" to "##########################" and "##########################" and "##USUWANIE GILDII##" and "##Zmienne: {PREFIX}, {PLAYER}, {TAG}, {NAME}"
        set yaml value "player-isnt-owner" from "guilds.messages" to "{PREFIX} &cNie jesteś liderem gildii!"
        set yaml value "broadcast-guild-delete" from "guilds.messages" to "{PREFIX} &7Gracz &3{PLAYER} &7usunął gildię &3{TAG}"
        set yaml value "no-info-tag-argument" from "guilds.messages" to "{PREFIX} &cPodaj tag gildii!"
        set the comments of yaml node "no-info-tag-argument" from "guilds.messages" to "##########################" and "##########################" and "##SPRAWDZANIE INFORMACJI O GILDII##" and "##Zmienne: {PREFIX}, {TAG}, {NAME}, {OWNER}, {MEMBERS}, {ONLINE-MEMBERS}, {DEPUTIES}"
        set yaml value "guild-info-doesnt-exists" from "guilds.messages" to "{PREFIX} &cGildia o podanym tagu nie istnieje!"
        set yaml list "guild-info-message" from "guilds.messages" to "{PREFIX} &7Informacje o gildii: &3{TAG} {PREFIX}" and "{PREFIX} &7TAG: &3{TAG}" and "{PREFIX} &7Nazwa: &3{NAME}" and "{PREFIX} &7Lider: &3{OWNER}" and "{PREFIX} &7Zastępcy: &3{DEPUTIES}" and "{PREFIX} &7Członkowie &8(&3{ONLINE-MEMBERS} online&8): &3{MEMBERS}"
        set yaml value "player-invite-doesnt-have-guild" from "guilds.messages" to "{PREFIX} &cNie jesteś w gildii!"
        set the comments of yaml node "player-invite-doesnt-have-guild" from "guilds.messages" to "##########################" and "##########################" and "##ZAPRASZANIE DO GILDII##" and "##Zmienne: {PREFIX}, {TAG}, {PLAYER}"
        set yaml value "player-invite-isnt-owner" from "guilds.messages" to "{PREFIX} &cNie jesteś liderem gildii"
        set yaml value "invite-no-player-argument" from "guilds.messages" to "{PREFIX} &cPodaj nick gracza!"
        set yaml value "player-is-offline" from "guilds.messages" to "{PREFIX} &cPodany gracz jest offline!"
        set yaml value "invite-player-argument-is-in-guild" from "guilds.messages" to "{PREFIX} &cPodany gracz jest już w gildii!"
        set yaml value "recive-invite-message" from "guilds.messages" to "{PREFIX} &7Otrzymałeś zaproszenie do gildii &3{TAG}&7!"
        set yaml value "send-invite-message" from "guilds.messages" to "{PREFIX} &7Wysłałeś zaproszenie graczowi &3{PLAYER}&7!"
        set yaml value "revoked-invite-argument-player" from "guilds.messages" to "{PREFIX} &7Zaproszenie do gildii &3{TAG} &7zostało cofnięte!"
        set yaml value "revoked-invite-player" from "guilds.messages" to "{PREFIX} &7Cofnąłeś zaproszenie graczowi &3{PLAYER} &7do gildii!"
        set yaml value "join-no-pending-invites" from "guilds.messages" to "{PREFIX} &cNie masz żadnych oczekujących zaproszeń do gildii!"
        set the comments of yaml node "join-no-pending-invites" from "guilds.messages" to "##########################" and "##########################" and "##DOŁĄCZANIE DO GILDII##" and "##Zmienne: {PREFIX}, {GUILDS}, {TAG}"
        set yaml value "join-pending-invites" from "guilds.messages" to "{PREFIX} &7Oczekujące zaproszenia do gildii: &3{GUILDS}"
        set yaml value "join-player-is-in-guild" from "guilds.messages" to "{PREFIX} &cJesteś już w gildii!"
        set yaml value "join-argument-guild-doesnt-exists" from "guilds.messages" to "{PREFIX} &cPodana gildia nie istnieje!"
        set yaml value "join-player-arent-invited" from "guilds.messages" to "{PREFIX} &cNie masz zaproszenia do gildii {TAG}!"
        set yaml value "broadcast-join-message" from "guilds.messages" to "{PREFIX} &7Gracz &3{PLAYER} &7dołączył do gildii &3{TAG}&7!"
        set yaml value "deputies-no-guild-executor" from "guilds.messages" to "{PREFIX} &cNie jesteś w gildii!"
        set the comments of yaml node "deputies-no-guild-executor" from "guilds.messages" to "##########################" and "##########################" and "##DAWANIE ZASTĘPCY##" and "##Zmienne: {PREFIX}, {PLAYER}, {TAG}"
        set yaml value "deputies-no-owner-rank" from "guilds.messages" to "{PREFIX} &cNie jesteś liderem gildii!"
        set yaml value "deputies-no-player-argument" from "guilds.messages" to "{PREFIX} &cPodaj nick gracza!"
        set yaml value "deputies-argument-player-no-guild" from "guilds.messages" to "{PREFIX} &cPodany gracz nie należy do żadnej gildii!"
        set yaml value "deputies-argument-player-is-in-another-guild" from "guilds.messages" to "{PREFIX} &cPodany gracz należy do innej gildii!"
        set yaml value "deputies-argument-player-are-executor" from "guilds.messages" to "{PREFIX} &cNie możesz mianować siebie samego jako zastępcę!"
        set yaml value "deputies-argument-player-removed-from-deputies" from "guilds.messages" to "{PREFIX} &7Odebrałeś graczowi &3{PLAYER} &7zastępcę w gildii &3{TAG}&7!"
        set yaml value "deputies-argument-player-removed-from-deputies-target" from "guilds.messages" to "{PREFIX} &7Nie jesteś już zastępcą w gildii &3{TAG}&7!"
        set yaml value "deputies-argument-player-got-deputy" from "guilds.messages" to "{PREFIX} &7Nadałeś graczowi &3{PLAYER} &7zastępcę w gildii &3{TAG}&7!"
        set yaml value "deputies-argument-player-got-deputy-target" from "guilds.messages" to "{PREFIX} &7Otrzymałeś zastępcę w gildii &3{TAG}&7!"
        save yaml "guilds.messages"
        unload yaml "guilds.messages"
    reloadConfig()
    reloadMessages()

command /g [<text>] [<text>] [<text>]:
    trigger:
        if arg-1 is not set:
            loop {guilds::messages::help-commands::*}:
                set {_msg} to coloured loop-value
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg}
            stop
        if arg-1 is "zaloz":
            if arg-2 is not set:
                set {_msg} to {guilds::messages::no-tag-argument}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            if length of arg-2 < {guilds::config::min-tag-length}:
                set {_msg} to {guilds::messages::min-tag-length}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                replace all "{MIN-TAG-LENGTH}" with "%{guilds::config::min-tag-length}%" in {_msg}
                send {_msg} to player
                stop
            if length of arg-2 > {guilds::config::max-tag-length}:
                set {_msg} to {guilds::messages::max-tag-length}
                replace all "{MAX-TAG-LENGTH}" with {guilds::config::max-tag-length} in {_msg}
                send {_msg} to player
                stop
            if arg-3 is not set:
                set {_msg} to {guilds::messages::no-name-argument}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            if length of arg-3 < {guilds::config::min-name-length}:
                set {_msg} to {guilds::messages::min-name-length}
                replace all "{MIN-NAME-LENGTH}" with {guilds::config::min-name-length} in {_msg}
                send {_msg} to player
                stop
            if length of arg-3 > {guilds::config::max-name-length}:
                set {_msg} to {guilds::messages::max-name-length}
                replace all "{MIN-NAME-LENGTH}" with {guilds::config::max-name-length} in {_msg}
                send {_msg} to player
                stop
            #sprawdz czy gracz jest w gildii
            if player has guild is true:
                set {_msg} to {guilds::messages::player-is-in-guild}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            #sprawdz czy gildia o podanym tagu nie istnieje
            if guild tag arg-2 exists is true:
                set {_msg} to {guilds::messages::guild-tag-exists}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            #sprawdz czy gildia o podanej nazwie nie istnieje
            if guild name arg-3 exists is true:
                set {_msg} to {guilds::messages::guild-name-exists}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            #sprawdz czy gracz ma przedmioty na gildię
            if {guilds::config::required-items::1} is not "none":
                loop {guilds::config::required-items::*}:
                    set {_i} to loop-value parsed as itemtype
                    if player doesn't have {_i}:
                        set {_msg} to {guilds::messages::required-item-error}
                        replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                        replace all "{ITEM}" with "%{_i}%" in {_msg}
                        send {_msg} to player
                        stop
            #sprawdz czy gracz może w tym miejscu założyć gildię
            #stwórz obszar gildii (corners)
            set {_1} to x-coordinate of player - 50
            set {_2} to z-coordinate of player - 50
            set {_c1} to location({_1}, 256, {_2})
            set {_1} to x-coordinate of player + 50
            set {_2} to z-coordinate of player - 50
            set {_c2} to location({_1}, 256, {_2})
            set {_1} to x-coordinate of player + 50
            set {_2} to z-coordinate of player + 50
            set {_c3} to location({_1}, -256, {_2})
            set {_1} to x-coordinate of player - 50
            set {_2} to z-coordinate of player + 50
            set {_c4} to location({_1}, 256, {_2})
            #sprawdz czy teren nie zajmie zablokowanych regionów
            if {guilds::config::blocked-regions::1} is not "none":
                loop {guilds::config::blocked-regions::*}:
                    #sprawdz czy rogi gildii nie stykają się
                    loop 4 times:
                        if "%region at {_c%loop-value-2%}%" contains loop-value-1:
                            set {_msg} to {guilds::messages::region-contain-blocked-region}
                            replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                            send {_msg} to player
                            stop
                    set {_l} to block at player's location
                    loop 50 times:
                        set {_b} to block loop-value-2 left of {_l}
                        if "%region at {_b}%" contain loop-value-1:
                            set {_msg} to {guilds::messages::region-contain-blocked-region}
                            replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                            send {_msg} to player
                            stop
                        set {_b} to block loop-value-2 right of {_l}
                        if "%region at {_b}%" contain loop-value-1:
                            set {_msg} to {guilds::messages::region-contain-blocked-region}
                            replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                            send {_msg} to player
                            stop
                        set {_b} to block loop-value-2 infront of {_l}
                        if "%region at {_b}%" contain loop-value-1:
                            set {_msg} to {guilds::messages::region-contain-blocked-region}
                            replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                            send {_msg} to player
                            stop
                        set {_b} to block loop-value-2 behind {_l}
                        if "%region at {_b}%" contain loop-value-1:
                            set {_msg} to {guilds::messages::region-contain-blocked-region}
                            replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                            send {_msg} to player
                            stop
            #sprawdz czy teren nie zetknie się z inną gildią
            loop 4 times:
                if "%region at {_c%loop-value%}%" contains "guild-":
                    set {_msg} to {guilds::messages::region-contain-another-guild}
                    replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                    send {_msg} to player
                    stop
            set {_l} to block at player's location
            loop 50 times:
                set {_b} to block loop-value left of {_l}
                if "%region at {_b}%" contain "guild-":
                    set {_msg} to {guilds::messages::region-contain-another-guild}
                    replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                    send {_msg} to player
                    stop
                set {_b} to block loop-value right of {_l}
                if "%region at {_b}%" contain "guild-":
                    set {_msg} to {guilds::messages::region-contain-another-guild}
                    replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                    send {_msg} to player
                    stop
                set {_b} to block loop-value infront of {_l}
                if "%region at {_b}%" contain "guild-":
                    set {_msg} to {guilds::messages::region-contain-another-guild}
                    replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                    send {_msg} to player
                    stop
                set {_b} to block loop-value behind {_l}
                if "%region at {_b}%" contain "guild-":
                    set {_msg} to {guilds::messages::region-contain-another-guild}
                    replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                    send {_msg} to player
                    stop
            #stwórz region
            define region "guild-%arg-2%" from {_c1} to {_c3} in player's world
            #zabierz przedmioty na gildie
            loop {guilds::config::required-items::*}:
                set {_i} to loop-value parsed as itemtype
                remove {_i} from player
            #załóż gildie
            load yaml "plugins/guilds/data/guilds/%arg-2%.yml" as "%arg-2%.guild"
            set yaml value "tag" from "%arg-2%.guild" to arg-2
            set yaml value "name" from "%arg-2%.guild" to arg-3
            set yaml value "owner" from "%arg-2%.guild" to player's name
            set yaml list "members" from "%arg-2%.guild" to player's name
            set yaml list "deputies" from "%arg-2%.guild" to "none"
            save yaml "%arg-2%.guild"
            unload yaml "%arg-2%.guild"
            #ustaw graczowi gildie
            load yaml "plugins/guilds/data/players/%player's name%.yml" as "%player's name%.player"
            set yaml value "guild" from "%player's name%.player" to arg-2
            save yaml "%player's name%.player"
            unload yaml "%player's name%.player"
            #daj belowname
            set {_format} to {guilds::config::belowname-tag-format}
            replace all "{TAG}" with arg-2 in {_format}
            execute console command "tab player %player% belowname %{_format}%"
            #ogłos założenie gildii
            set {_msg} to {guilds::messages::broadcast-guild-create}
            replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
            replace all "{PLAYER}" with player's name in {_msg}
            replace all "{TAG}" with arg-2 in {_msg}
            replace all "{NAME}" with arg-3 in {_msg}
            broadcast {_msg}
            stop
        if arg-1 is "usun":
            #sprawdz czy gracz ma gildie
            if player has guild is false:
                set {_msg} to {guilds::messages::player-hasnt-guild}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            set {_g} to player guild tag
            set {_owner} to false
            load yaml "plugins/guilds/data/guilds/%{_g}%.yml" as "%{_g}%.guild"
            set {_name} to yaml value "name" from "%{_g}%.guild"
            if yaml value "owner" from "%{_g}%.guild" is "%player%":
                set {_owner} to true
            unload yaml "%{_g}%.guild"
            if {_owner} is false:
                set {_msg} to {guilds::messages::player-isnt-owner}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            execute console command "rg remove guild-%{_g}% -w %player's world%"
            load yaml "plugins/guilds/data/guilds/%{_g}%.yml" as "%{_g}%.guild"
            set {_members::*} to yaml list "members" from "%{_g}%.guild"
            loop {_members::*}:
                load yaml "plugins/guilds/data/players/%loop-value%.yml" as "%loop-value%.player"
                set yaml value "guild" from "%loop-value%.player" to "brak"
                save yaml "%loop-value%.player"
                unload yaml "%loop-value%.player"
                set {_p} to loop-value parsed as player
                if {_p} is online:
                    execute console command "tab player %{_p}% belowname"
            unload yaml "%{_g}%.guild"
            set {_msg} to {guilds::messages::broadcast-guild-delete}
            replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
            replace all "{PLAYER}" with "%player%" in {_msg}
            replace all "{TAG}" with {_g} in {_msg}
            replace all "{NAME}" with {_name} in {_msg}
            broadcast {_msg}
            load yaml "plugins/guilds/data/guilds/%{_g}%.yml" as "%{_g}%.guild"
            delete yaml "%{_g}%.guild"
            unload yaml "%{_g}%.guild"
            stop
        if arg-1 is "itemy":
            close player's inventory
            set {_guiName} to {guilds::config::required-items-gui-name}
            replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_guiName}
            set {_rows} to size of {guilds::config::required-items::*}
            set {_rows} to rounded up {_rows} / 9
            open chest inventory with {_rows} rows named {_guiName} to player
            set {_slot} to 0
            loop {guilds::config::required-items::*}:
                set slot {_slot} of player's current inventory to loop-value parsed as itemtype
                add 1 to {_slot}
            stop
        if arg-1 is "info":
            if arg-2 is not set:
                if player guild tag is "brak":
                    set {_msg} to {guilds::messages::no-info-tag-argument}
                    replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                    send {_msg} to player
                    stop
                else:
                    set {_g} to player guild tag
            else:
                set {_g} to arg-2
                # sprawdz czy gildia istnieje
                if guild tag {_g} exists is false:
                    set {_msg} to {guilds::messages::guild-info-doesnt-exists}
                    replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                    send {_msg} to player
                    stop
            # pobierz informacje o gildii
            load yaml "plugins/guilds/data/guilds/%{_g}%.yml" as "%{_g}%.guild"
            set {_tag} to yaml value "tag" from "%{_g}%.guild"
            set {_name} to yaml value "name" from "%{_g}%.guild"
            set {_owner} to yaml value "owner" from "%{_g}%.guild"
            set {_members::*} to yaml list "members" from "%{_g}%.guild"
            set {_deputies::*} to yaml list "deputies" from "%{_g}%.guild"
            loop {_members::*}:
                set {_p} to loop-value parsed as player
                if {_p} is online:
                    add {_p} to {_onlineMembers::*}
            unload yaml "%{_g}%.guild"
            # wyswietl inforamcje o gildii
            loop {guilds::messages::guild-info-message::*}:
                set {_msg} to coloured loop-value
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                replace all "{TAG}" with {_tag} in {_msg}
                replace all "{NAME}" with {_name} in {_msg}
                replace all "{OWNER}" with {_owner} in {_msg}
                set {_onlineMembersCount} to size of {_onlineMembers::*}
                replace all "{ONLINE-MEMBERS}" with "%{_onlineMembersCount}%" in {_msg}
                set {_members} to "%{_members::*}%"
                replace all " and " with ", " in {_members}
                replace all "{MEMBERS}" with {_members} in {_msg}
                if {_deputies::1} is not "none":
                    set {_deputies} to "%{_deputies::*}%"
                    replace all " and " with ", " in {_deputies}
                else:
                    set {_deputies} to "brak"
                replace all "{DEPUTIES}" with {_deputies} in {_msg}
                send {_msg} to player
            stop
        if arg-1 is "zapros":
            #sprawdz czy gracz ma gildie
            load yaml "plugins/guilds/data/players/%player's name%.yml" as "%player's name%.player"
            set {_g} to yaml value "guild" from "%player's name%.player"
            unload yaml "%player's name%.player"
            if {_g} is "brak":
                set {_msg} to {guilds::messages::player-invite-doesnt-have-guild}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            #sprawdz lidera
            load yaml "plugins/guilds/data/guilds/%{_g}%.yml" as "%{_g}%.guild"
            set {_owner} to yaml value "owner" from "%{_g}%.guild"
            unload yaml "%{_g}%.guild"
            if {_owner} is not player's name:
                set {_msg} to {guilds::messages::player-invite-isnt-owner}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            if arg-2 is not set:
                set {_msg} to {guilds::messages::invite-no-player-argument}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            set {_p} to arg-2 parsed as player
            #sprawdz czy jest online
            if {_p} is offline:
                set {_msg} to {guilds::messages::player-is-offline}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            #sprawdz czy podany gracz ma gildie
            load yaml "plugins/guilds/data/players/%{_p}%.yml" as "%{_p}%.player"
            set {_arg-g} to yaml value "guild" from "%{_p}%.player"
            unload yaml "%{_p}%.player"
            if {_arg-g} is not "brak":
                set {_msg} to {guilds::messages::invite-player-argument-is-in-guild}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            set {_owner-tag} to player guild tag
            #sprawdz czy podany gracz jest juz dodaany do gildii
            set {_return} to false
            loop {guildsData::pendingInvites::%{_p}%::*}:
                if loop-value is {_owner-tag}:
                    set {_return} to true
                    stop loop
            #false - nie jest dodany
            if {_return} is false:
                #dodaj do gildii
                add {_owner-tag} to {guildsData::pendingInvites::%{_p}%::*}
                #wyslij wiadomosc o dodaniu
                #do dodanego gracza
                set {_msg} to {guilds::messages::recive-invite-message}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                replace all "{TAG}" with {_owner-tag} in {_msg}
                send {_msg} to {_p}
                #do dodającego
                set {_msg} to {guilds::messages::send-invite-message}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                replace all "{PLAYER}" with "%{_p}%" in {_msg}
                send {_msg} to player
            #true - jest dodany
            if {_return} is true:
                #usun zaproszenie
                remove {_owner-tag} from {guildsData::pendingInvites::%{_p}%::*}
                #wyslij wiadomosc o cofeniciu
                #do podanego gracza
                set {_msg} to {guilds::messages::revoked-invite-argument-player}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                replace all "{TAG}" with {_owner-tag} in {_msg}
                send {_msg} to {_p}
                #do dodającego
                set {_msg} to {guilds::messages::revoked-invite-player}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                replace all "{PLAYER}" with "%{_p}%" in {_msg}
                send {_msg} to player
            stop
        if arg-1 is "dolacz":
            #sprawdz czy jest w gildii
            load yaml "plugins/guilds/data/players/%player%.yml" as "%player%.player"
            set {_g} to yaml value "guild" from "%player%.player"
            unload yaml "%player%.player"
            if {_g} is not "brak":
                set {_msg} to {guilds::messages::join-player-is-in-guild}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            if arg-2 is not set:
                if {guildsData::pendingInvites::%player%::*} is not set:
                    set {_msg} to {guilds::messages::join-no-pending-invites}
                    replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                    send {_msg} to player
                    stop
                else:
                    set {_msg} to {guilds::messages::join-pending-invites}
                    replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                    set {_guilds} to "%{guildsData::pendingInvites::%player%::*}%"
                    replace all " and " with ", " in {_guilds}
                    replace all "{GUILDS}" with {_guilds} in {_msg}
                    send {_msg} to player
                    stop
            set {_join-g} to arg-2
            load yaml "plugins/guilds/data/guilds/%{_join-g}%.yml" as "%{_join-g}%.guild"
            set {_join-owner} to yaml value "owner" from "%{_join-g}%.guild"
            unload yaml "%{_join-g}%.guild"
            if {_join-owner} is not set:
                set {_msg} to {guilds::messages::join-argument-guild-doesnt-exists}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            set {_return} to false
            loop {guildsData::pendingInvites::%player%::*}:
                if loop-value is arg-2:
                    set {_return} to true
                    stop loop
            if {_return} is false:
                set {_msg} to {guilds::messages::join-player-arent-invited}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                replace all "{TAG}" with arg-2 in {_msg}
                send {_msg} to player
                stop
            #dodaj gracza
            load yaml "plugins/guilds/data/players/%player%.yml" as "%player%.player"
            set yaml value "guild" from "%player%.player" to arg-2
            save yaml "%player%.player"
            unload yaml "%player%.player"
            #daj belowname
            set {_format} to {guilds::config::belowname-tag-format}
            replace all "{TAG}" with arg-2 in {_format}
            execute console command "tab player %player% belowname %{_format}%"
            #dodaj gracza do yml gildii
            load yaml "plugins/guilds/data/guilds/%arg-2%.yml" as "%arg-2%.guild"
            set {_list::*} to yaml list "members" from "%arg-2%.guild"
            add "%player%" to {_list::*}
            set yaml list "members" from "%arg-2%.guild" to {_list::*}
            save yaml "%arg-2%.guild"
            unload yaml "%arg-2%.guild"
            #oglos
            set {_msg} to {guilds::messages::broadcast-join-message}
            replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
            replace all "{PLAYER}" with "%player%" in {_msg}
            replace all "{TAG}" with arg-2 in {_msg}
            broadcast {_msg}
            stop
        if arg-1 is "zastepca":
            #sprawdz czy executor jest w gildii - passed
            load yaml "plugins/guilds/data/players/%player%.yml" as "%player%.player"
            set {_g} to yaml value "guild" from "%player%.player"
            unload yaml "%player%.player"
            if {_g} is "brak":
                set {_msg} to {guilds::messages::deputies-no-guild-executor}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            #sprawdz czy executor jest liderem - passed
            load yaml "plugins/guilds/data/guilds/%{_g}%.yml" as "%{_g}%.guild"
            set {_owner} to yaml value "owner" from "%{_g}%.guild"
            set {_deputies::*} to yaml list "deputies" from "%{_g}%.guild"
            unload yaml "%{_g}%.guild"
            if {_owner} is not player's name:
                set {_msg} to {guilds::messages::deputies-no-owner-rank}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            #sprawdz czy executor podal gracza - passed
            if arg-2 is not set:
                set {_msg} to {guilds::messages::deputies-no-player-argument}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            set {_p} to arg-2 parsed as player
            #sprawdz czy podany gracz jest w gildii - passed
            load yaml "plugins/guilds/data/players/%{_p}%.yml" as "%{_p}%.player"
            set {_argument-g} to yaml value "guild" from "%{_p}%.player"
            unload yaml "%{_p}%.player"
            if {_argument-g} is "brak":
                set {_msg} to {guilds::messages::deputies-argument-player-no-guild}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            #sprawdz czy sa w tej samej gildii - passed
            if {_argument-g} is not {_g}:
                set {_msg} to {guilds::messages::deputies-argument-player-is-in-another-guild}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            #sprawdz czy executor podal swój nick
            if {_p} is player's name:
                set {_msg} to {guilds::messages::deputies-argument-player-are-executor}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                send {_msg} to player
                stop
            set {_return} to false
            loop {_deputies::*}:
                if loop-value is {_p}:
                    set {_return} to true
                    stop loop
            #jest zastępcą - więc zabierz
            if {_return} is true:
                remove {_p} from {_deputies::*}
                if {_deputies::*} is not set:
                    add "none" to {_deputies::*}
                load yaml "plugins/guilds/data/guilds/%{_g}%.yml" as "%{_g}%.guild"
                set yaml list "deputies" from "%{_g}%.guild" to {_deputies::*}
                save yaml "%{_g}%.guild"
                unload yaml "%{_g}%.guild"
                #wiadomość
                set {_msg} to {guilds::messages::deputies-argument-player-removed-from-deputies}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                replace all "{PLAYER}" with "%{_p}%" in {_msg}
                replace all "{TAG}" with {_g} in {_msg}
                send {_msg} to player
                #do bylego zastepcy
                set {_msg} to {guilds::messages::deputies-argument-player-removed-from-deputies-target}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                replace all "{TAG}" with {_g} in {_msg}
                send {_msg} to {_p}
            #nie jest zastępcą więc dodaj
            else:
                if {_deputies::1} is "none":
                    remove "none" from {_deputies::*}
                add {_p} to {_deputies::*}
                load yaml "plugins/guilds/data/guilds/%{_g}%.yml" as "%{_g}%.guild"
                set yaml list "deputies" from "%{_g}%.guild" to {_deputies::*}
                save yaml "%{_g}%.guild"
                unload yaml "%{_g}%.guild"
                #wiadomosc
                set {_msg} to {guilds::messages::deputies-argument-player-got-deputy}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                replace all "{PLAYER}" with "%{_p}%" in {_msg}
                replace all "{TAG}" with {_g} in {_msg}
                send {_msg} to player
                #do goscia
                set {_msg} to {guilds::messages::deputies-argument-player-got-deputy-target}
                replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_msg}
                replace all "{TAG}" with {_g} in {_msg}
                send {_msg} to {_p}
            stop

command /ga [<text>]:
    permission: ga.usage
    permission message: &8✦ &cNie masz dostępu do tej komendy!
    trigger:
        if arg-1 is not set:
            send "%{guilds::config::chat-prefix}% &7Pomoc dotycząca ga"
            send "%{guilds::config::chat-prefix}% &c/ga reload &8- &7przeładowuje plik konfiguracyjny"
            stop
        if arg-1 is "reload":
            reloadConfig()
            reloadMessages()
            send "%{guilds::config::chat-prefix}% &aPrzeładowano &2skGuilds &apomyślnie!"
            stop

on inventory click:
    set {_guiName} to {guilds::config::required-items-gui-name}
    replace all "{PREFIX}" with {guilds::config::chat-prefix} in {_guiName}
    if name of player's current inventory is {_guiName}:
        if clicked inventory is not player's inventory:
            cancel event

on quit:
    clear {guildsData::pendingInvites::%player%::*}