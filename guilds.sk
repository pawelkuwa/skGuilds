expression %player% has guild:
    return type: boolean
    get:
        set {_return} to false
        load yaml "plugins/guilds/data/players/%expression-1%.yml" as "%expression-1%.player"
        set {_return} to yaml value "guild" from "%expression-1%.player"
        unload yaml "%expression-1%.player"
        if {_return} is not "brak":
            set {_return} to true
        return {_return}

expression guild tag %text% exists:
    return type: boolean
    get:
        if yaml file "plugins/guilds/data/guilds/%expression-1%.yml" doesn't exist:
            set {_return} to false
        else:
            set {_return} to true
        return {_return}

expression guild name %text% exists:
    return type: boolean
    get:
        set {_return} to false
        load all yaml from directory "plugins/guilds/data/guilds"
        loop all of the currently loaded yaml files:
            if loop-value contains "plugins/guilds/data/guilds/":
                if yaml value "name" from loop-value is expression-1:
                    set {_return} to true
            unload loop-value
        return {_return}

on join:
    if yaml file "plugins/guilds/data/players/%player%.yml" doesn't exist:
        load yaml "plugins/guilds/data/players/%player%.yml" as "%player%.player"
        set yaml value "guild" from "%player%.player" to "brak"
        set yaml value "points" from "%player%.player" to 1000
        set yaml value "kills" from "%player%.player" to 0
        set yaml value "deaths" from "%player%.player" to 0
        save yaml "%player%.player"
        unload yaml "%player%.player"

function reloadConfig():
    load yaml "plugins/guilds/config.yml" as "guilds.config"
    loop all node from "guilds.config":
        set {guilds::config::%loop-value%} to yaml value loop-value from "guilds.config"
    unload yaml "guilds.config"

on load:
    if yaml file "plugins/guilds/config.yml" doesn't exist:
        load yaml "plugins/guilds/config.yml" as "guilds.config"
        set yaml value "chat-prefix" from "guilds.config" to "✦"
        set the comments of yaml node "chat-prefix" from "guilds.config" to "Jak na chacie ma wyglądać prefix przed wiadomościami?"
        set yaml value "min-tag-length" from "guilds.config" to 2
        set the comments of yaml node "min-tag-length" from "guilds.config" to "Ile minimalnie musi zawierać tag gildii?"
        set yaml value "max-tag-length" from "guilds.config" to 5
        set the comments of yaml node "max-tag-length" from "guilds.config" to "Ile maksymalnie może mieć znaków tag gildii?"
        set yaml value "min-name-length" from "guilds.config" to 5
        set the comments of yaml node "min-name-length" from "guilds.config" to "Ile minimalnie musi zawierać nazwa gildii?"
        set yaml value "max-name-length" from "guilds.config" to 15
        set the comments of yaml node "max-name-length" from "guilds.config" to "Ile maksymalnie może zawierać zanków tag gildii?"
        save yaml "guilds.config"
        unload yaml "guilds.config"
    reloadConfig()

command /g [<text>] [<text>] [<text>]:
    trigger:
        if arg-1 is not set:
            send "%{guilds::config::chat-prefix}% &8&m-=-=-=-=-=&r &8( &3&lGILDIE &8)&8&m=-=-=-=-=-&r %{guilds::config::chat-prefix}%"
            send "%{guilds::config::chat-prefix}% &3/g zaloz [tag] [nazwa] &8- &7tworzy gildie"
            stop
        if arg-1 is "zaloz":
            if arg-2 is not set:
                send "%{guilds::config::chat-prefix}% &cPodaj tag gildii!"
                stop
            if length of arg-2 < {guilds::config::min-tag-length}:
                send "%{guilds::config::chat-prefix}% &cTag gildii musi zawierać przynajmniej %{guilds::config::min-tag-length}% znaki!"
                stop
            if length of arg-2 > {guilds::config::max-tag-length}:
                send "%{guilds::config::chat-prefix}% &cTag gildii nie może zawierać więcej niż %{guilds::config::max-tag-length}% znaki!"
                stop
            if arg-3 is not set:
                send "%{guilds::config::chat-prefix}% &cPodaj nazwę gildii!"
                stop
            if length of arg-3 < {guilds::config::min-name-length}:
                send "%{guilds::config::chat-prefix}% &cNazwa gildii musi zawierać przynajmniej %{guilds::config::min-name-length}% znaków!"
                stop
            if length of arg-3 > {guilds::config::max-name-length}:
                send "%{guilds::config::chat-prefix}% &cNazwa gildii nie może zawierać więcej niż %{guilds::config::max-name-length}% znaków!"
                stop
            #sprawdz czy gracz jest w gildii
            if player has guild is true:
                send "%{guilds::config::chat-prefix}% &cJesteś już w gildii!"
                stop
            #sprawdz czy gildia o podanym tagu nie istnieje
            if guild tag arg-2 exists is true:
                send "%{guilds::config::chat-prefix}% &cGildia o podanym tagu istnieje!"
                stop
            #sprawdz czy gildia o podanej nazwie nie istnieje
            if guild name arg-3 exists is true:
                send "%{guilds::config::chat-prefix}% &cGildia o podanej nazwie istnieje!" 
            #sprawdz czy gracz może w tym miejscu założyć gildię
            #załóż gildie
            load yaml "plugins/guilds/data/guilds/%arg-2%.yml" as "%arg-2%.guild"
            set yaml value "tag" from "%arg-2%.guild" to arg-2
            set yaml value "name" from "%arg-2%.guild" to arg-3
            set yaml value "owner" from "%arg-2%.guild" to player's name
            set yaml list "members" from "%arg-2%.guild" to player's name
            save yaml "%arg-2%.guild"
            unload yaml "%arg-2%.guild"
            #ustaw graczowi gildie
            load yaml "plugins/guilds/data/players/%player's name%.yml" as "%player's name%.player"
            set yaml value "guild" from "%player's name%.player" to arg-2
            save yaml "%player's name%.player"
            unload yaml "%player's name%.player"
            #ogłos założenie gildii
            broadcast "%{guilds::config::chat-prefix}% &7Gracz &3%player's name% &7założył gildie o tagu &3%arg-2% &7i nazwie &3%arg-3%"
            stop

command /ga [<text>]:
    permission: ga.usage
    permission message: &8✦ &cNie masz dostępu do tej komendy!
    trigger:
        if arg-1 is not set:
            send "%{guilds::config::chat-prefix}% &7Pomoc dotycząca ga"
            send "%{guilds::config::chat-prefix}% &c/ga reload &8- &7przeładowuje plik konfiguracyjny"
            stop
        if arg-1 is "reload":
            reloadConfig()
            send "%{guilds::config::chat-prefix}% &aPrzeładowano &2skGuilds &apomyślnie!"
            stop